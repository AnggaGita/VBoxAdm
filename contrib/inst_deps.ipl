#!/usr/bin/perl
use strict;
use warnings;
use 5.9.0;

use English qw(-no_match_vars);

use CPAN;

use Term::ANSIColor qw(:constants);
$Term::ANSIColor::AUTORESET = 1;

# module syntax: Module::Name/MinVersion/Required/DEB/RPM
# TODO find debian/redhat/suse packages if avail. - http://www.rpmseek.com/index.html
my @modules = qw(
  Carp/0/1/perl-modules/
  CGI::Application/0/1/libcgi-application-perl/perl-CGI-Application
  CGI::Application::Plugin::Authentication/0/1/libcgi-application-extra-plugin-bundle-perl/perl-CGI-Application-Plugin-Authentication
  CGI::Application::Plugin::DBH/0/1/libcgi-application-basic-plugin-bundle-perl/perl-CGI-Application-Plugin-DBH
  CGI::Application::Plugin::Redirect/0/1/libcgi-application-basic-plugin-bundle-perl/perl-CGI-Application-Plugin-Redirect
  CGI::Application::Plugin::RequireSSL/0/1/libcgi-application-extra-plugin-bundle-perl/perl-CGI-Application-Plugin-RequireSSL
  CGI::Application::Plugin::Session/0/1/libcgi-application-basic-plugin-bundle-perl/perl-CGI-Application-Plugin-Session
  CGI::Application::Plugin::TT/0/1/libcgi-application-extra-plugin-bundle-perl/perl-CGI-Application-Plugin-TT
  CGI::Carp/0/1/perl-modules///
  CGI::Fast/0/0/libcgi-fast-perl///
  Config::Std/0/1/libconfig-std-perl/perl-Config-Std/
  CPAN/0/1/perl-modules///
  Crypt::GeneratePassword/0/1/libcrypt-generatepassword-perl/perl-Crypt-GeneratePassword//
  Crypt::OpenSSL::RSA/0/1/libcrypt-openssl-rsa-perl/perl-Crypt-OpenSSL-RSA//
  Cwd/0
  Data::Dumper/0
  Data::Page/0
  Data::Password/0
  DBI/0/1/libdbi-perl/perl-DBI
  Digest::MD5/0/1//perl-Digest-MD5
  Digest::Perl::MD5/0/1/libdigest-perl-md5/
  Digest::SHA/0/1/libdigest-sha-perl/perl-Digest-SHA
  File::Basename/0
  File::Temp/0
  Getopt::Long/0
  HTML::Clean/0/1/libhtml-clean-perl/perl-HTML-Clean
  IO::File/0
  IO::Socket::INET6/0
  List::Util/0
  Mail::SpamAssassin/0/1/spamassassin/perl-Mail-SpamAssassin
  MIME::Base64/0
  Net::Domain/0
  Net::IMAP::Client/0
  Net::POP3/0
  Net::SMTP/0
  Readonly/0
  Sys::Syslog/0
  Template/0
  Test::More/0
  Text::CSV_XS/0
  Time::HiRes/0
);
my $tpl = "%40s [%4s]\n";

print "Checking required Perl modules ...\n";
foreach my $modLine (@modules) {
    my ( $module, $version, $req, $debpkg, $rpmpkg ) = split /\//, $modLine;
    my $ver = &get_version($module);

    if ( $ver && $ver ge $version ) {

        # installed and version ok
        printf( $tpl, $module, GREEN "OK" );
    }
    else {

        # missing ... try to install
        my $os = &get_os_type();
        if ( $os eq 'debian' && $debpkg && &install_deb($debpkg) ) {
            printf( $tpl, $module, BOLD YELLOW "INSTALLED" );
        }
        elsif ( $os eq 'redhat' && $rpmpkg && &install_yum($rpmpkg) ) {
            printf( $tpl, $module, BOLD YELLOW "INSTALLED" );
        }
        elsif ( $os eq 'suse' && $rpmpkg && &install_zypper($rpmpkg) ) {
            printf( $tpl, $module, BOLD YELLOW "INSTALLED" );
        }
        else {
            if ( &install_cpan($module) ) {
                printf( $tpl, $module, BOLD YELLOW "INSTALLED" );
            }
            else {
                printf( $tpl, $module, BOLD RED "ERROR" );
            }
        }
    }
}

sub get_version {
    my $module = shift;
    my $ver;

    no warnings 'once';
    open OLDOUT, ">&STDOUT";
    open OLDERR, ">&STDERR";
    open STDIN,  '<', '/dev/null';
    open STDOUT, '>', '/dev/null';
    open STDERR, '>', '/dev/null';
    $ver = eval("use $module; $module->VERSION;");
    open STDOUT, ">&OLDOUT";
    open STDERR, ">&OLDERR";
    use warnings 'once';

    return $ver;
}

sub get_os_type {
    my %dist_files = (
        '/etc/arch-release'      => 'unix',
        '/etc/debian_version'    => 'debian',
        '/etc/fedora-release'    => 'redhat',
        '/etc/gentoo-release'    => 'unix',
        '/etc/knoppix_version'   => 'debian',
        '/etc/mandriva-release'  => 'unix',
        '/etc/mandrake-release'  => 'unix',
        '/etc/redhat-release'    => 'redhat',
        '/etc/slackware-version' => 'unix',
        '/etc/SuSE-release'      => 'suse',
        '/etc/trustix-release'   => 'unix',
        '/etc/ubuntu_version'    => 'debian',
        '/etc/vmware-release'    => 'vmware',
        '/etc/issue'             => 'unix',
    );

    if ( $^O eq 'MSWin32' ) {
        return 'windows';
    }
    elsif ( $^O ne 'linux' ) {
        return 'unix';
    }
    else {
        foreach my $dist_file ( keys %dist_files ) {
            if ( -f $dist_file ) {
                return $dist_files{$dist_file};
            }
        }
        return 'unix';
    }
}

sub install_deb {
    my $pkg = shift;
    my $cmd = "apt-get --yes --assume-yes install " . $pkg . " >>/tmp/vboxadm-inst_deps.out 2>&1";
    my $rv  = system($cmd) >> 8;
    if ( $rv == 0 ) {
        return 1;
    }
    else {
        return;
    }
}

sub install_yum {
    my $pkg = shift;
    my $cmd = "yum -y install " . $pkg . " >>/tmp/vboxadm-inst_deps.out 2>&1";
    my $rv  = system($cmd) >> 8;
    if ( $rv == 0 ) {
        return 1;
    }
    else {
        return;
    }
}

sub install_zypper {
    my $pkg = shift;
    my $cmd = "zypper -y install " . $pkg . " >>/tmp/vboxadm-inst_deps.out 2>&1";
    my $rv  = system($cmd) >> 8;
    if ( $rv == 0 ) {
        return 1;
    }
    else {
        return;
    }
}

sub install_cpan {
    my $module = shift;
    return CPAN::Shell->install($module);
}

sub install_ppm {
    my $module = shift;

    $module =~ s/::/-/g;

    my $cmd = "ppm install " . $module;
    my $rv  = system($cmd) >> 8;
    if ( $rv == 0 ) {
        return 1;
    }
    else {
        return;
    }
}
